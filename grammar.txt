formulae
    ::= declaration*

declaration
    ::= 'def' identifier '=' expression
      | 'const' identifier '=' expression
      | 'def' identifier '(' identifier (',' identifier)* ')'
      | 'var' identifier ('=' expression)?

expression
    ::= ternary

ternary
    ::= disjunctive '?' disjunctive ':' ternary
      | disjunctive

disjunctive
    ::= disjunctive 'or' conjunctive
      | conjunctive

conjunctive
    ::= conjunctive 'and' relational
      | relational

relational
    ::= additive ('<' | '>' | '<=' | '>=' | '==' | '!=') additive
      | additive

additive
    ::= additive ('+' | '-') multiplicative
      | multiplicative

multiplicative
    ::= multiplicative ('*' | '/' | 'mod') prefix
      | prefix

prefix
    ::= ('not' | '-') prefix
      | exponentiation

exponentiation
    ::= postfix '^' prefix
      | postfix

postfix
    ::= applicative '%'
      | applicative

applicative
    ::= identifier '(' expression (',' expression)* ')'
      | primary

primary
    ::= identifier
      | number
      | string
      | '(' expression ')'
